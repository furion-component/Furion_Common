plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'com.huibur.furion'
version = '1.0.0'

repositories {
    mavenCentral()
}

ext{
    guavaVer = '28.2-jre'
    commonsLang3Ver = '3.9'
    commonsCodecVer = '1.14'
    commonsIOVer = '2.6'
    commonsFileUploadVer = '1.4'
    commonsBeanUtilsVer = '1.9.4'
    commonsTextVer = '1.8'
    pagehelperVer = '5.1.11'
    dozerVer = '5.5.1'
    mapstructVer = '1.3.1.Final'
}

dependencies {
    implementation "com.google.guava:guava:$guavaVer"
    implementation "org.apache.commons:commons-lang3:$commonsLang3Ver"
    implementation "commons-codec:commons-codec:$commonsCodecVer"
    implementation "commons-io:commons-io:$commonsIOVer"
    implementation "commons-fileupload:commons-fileupload:$commonsFileUploadVer"
    implementation "commons-beanutils:commons-beanutils:$commonsBeanUtilsVer"
    implementation "org.apache.commons:commons-text:$commonsTextVer"
    implementation 'org.apache.poi:poi:4.1.0'
    implementation 'org.springframework:spring-beans:5.1.7.RELEASE'
    implementation 'de.ruedigermoeller:fst:2.57'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.9.7'
    implementation 'org.springframework:spring-web:5.0.10.RELEASE'
    implementation 'dom4j:dom4j:1.6.1'
    implementation 'com.bladejava:blade-patchca:1.1.0'
    implementation 'net.sf.jmimemagic:jmimemagic:0.1.5'
    implementation 'org.apache.ant:ant:1.9.7'
    implementation 'com.belerweb:pinyin4j:2.5.1'
    implementation 'org.apache.commons:commons-email:1.5'
    implementation 'org.apache.poi:poi-ooxml:3.15'
    implementation 'eu.bitwalker:UserAgentUtils:1.21'
    implementation 'org.apache.httpcomponents:httpclient:4.5.3'
    implementation 'net.coobird:thumbnailator:0.4.8'
    implementation "net.sf.dozer:dozer:$dozerVer"
    implementation "org.mapstruct:mapstruct:$mapstructVer"
    implementation "com.github.pagehelper:pagehelper:$pagehelperVer"
    compileOnly 'javax.servlet:javax.servlet-api:4.0.1'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'furion-common'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'furion common'
                description = 'a common module for java web'
                url = 'https://github.com/furion-component/furion-common.git'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                organization {
                    name = 'HuiBur'
                    url = 'https://www.huibur.com'
                }
                developers {
                    developer {
                        id = 'wenzhijie'
                        name = 'wenzhijie'
                        email = 'wenzhi_jie@126.com'
                        organization = 'HuiBur'
                        organizationUrl = 'https://www.huibur.com'
                        roles = ['owner', 'developer']
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/furion-component/furion-common'
                    developerConnection = 'scm:git:ssh://git@github.com:furion-component/furion-common.git'
                    url = 'https://github.com/furion-component/furion-common.git'
                }
            }
        }
    }

    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    options{
        encoding "UTF-8"
        charSet 'UTF-8'
    }
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}